/*
 * Copyright (c) 2025, WSO2 LLC. (https://www.wso2.com).
 *
 * WSO2 LLC. licenses this file to you under the Apache License,
 * Version 2.0 (the "License"); you may not use this file except
 * in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied. See the License for the
 * specific language governing permissions and limitations
 * under the License.
 */

plugins {
    id 'java'
    id 'jacoco'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

group = project.group
version = project.version

repositories {
    mavenCentral()
    maven {
        url = 'https://maven.wso2.org/nexus/content/repositories/releases/'
    }

    maven {
        url = 'https://maven.wso2.org/nexus/content/groups/wso2-public/'
    }

    maven {
        url = 'https://repo.maven.apache.org/maven2'
    }
    maven {
        url = 'https://maven.pkg.github.com/ballerina-platform/*'
        credentials {
            username System.getenv("packageUser")
            password System.getenv("packagePAT")
        }
    }
}

configurations {
    // Configuration for MI runtime dependencies that should be packaged as separate JARs
    // Disable transitive dependencies - we only want the JARs we explicitly specify
    miRuntimeLibs {
        transitive = false
    }
    // Configuration for dependencies to be included in the shadow JAR
    shadowIncludes
}

dependencies {
    compileOnly "org.ballerinalang:ballerina-cli:${ballerinaLangVersion}"
    implementation 'info.picocli:picocli:4.0.1'
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'commons-io:commons-io:2.20.0'
    compileOnly "org.ballerinalang:ballerina-lang:${ballerinaLangVersion}"
    compileOnly group: 'org.ballerinalang', name: 'ballerina-parser', version: "${ballerinaLangVersion}"
    compileOnly "org.ballerinalang:ballerina-tools-api:${ballerinaLangVersion}"
    implementation "org.ballerinalang:diagram-util:${ballerinaLangVersion}"
    implementation "com.github.jknack:handlebars:${handlebarsVersion}"
    implementation "org.apache.commons:commons-compress:${commonsCompressVersion}"
    implementation "org.ow2.asm:asm:${asmVersion}"
    implementation project(':mi-native')
    implementation "org.apache.synapse:synapse-core:${synapseVersion}"
    implementation "com.networknt:json-schema-validator:${jsonSchemaValidatorVersion}"

    // MI runtime dependencies - these will be copied as separate JARs to lib directory
    miRuntimeLibs project(':mi-native')
    miRuntimeLibs "org.wso2.carbon.module:module-core:${carbonModuleCoreVersion}"
    miRuntimeLibs "org.wso2.integration.connector.core:mi-connector-core:${miConnectorCoreVersion}"

    // Dependencies to be included in shadow JAR
    shadowIncludes "com.github.jknack:handlebars:${handlebarsVersion}"
    shadowIncludes "org.antlr:antlr4-runtime:${antlrRuntimeVersion}"
    shadowIncludes "org.apache.synapse:synapse-core:${synapseVersion}"
    shadowIncludes "com.networknt:json-schema-validator:${jsonSchemaValidatorVersion}"

    testImplementation 'org.testng:testng:7.7.0'
    testImplementation 'org.mockito:mockito-core:5.3.1'
    testImplementation 'org.mockito:mockito-inline:5.2.0'
}

test {
    useTestNG()
}

task copyMiRuntimeLibs(type: Copy) {
    from configurations.miRuntimeLibs
    into 'src/main/resources/lib'
}

processResources.dependsOn copyMiRuntimeLibs

shadowJar {
    archiveBaseName.set('tool-migen-cli')
    archiveClassifier.set('')
    from sourceSets.main.output
    configurations = [project.configurations.shadowIncludes]
    zip64 = true
    mergeServiceFiles()

    // Exclude slf4j dependencies
    dependencies {
        exclude(dependency('org.slf4j:.*'))
    }

    // Shadow JAR treats .jar files as dependencies to merge, so we need to add them post-processing
    doLast {
        ant.zip(destfile: archiveFile.get(), update: true) {
            zipfileset(dir: "${buildDir}/resources/main/lib", prefix: 'lib') {
                include(name: '*.jar')
                include(name: '*.mar')
            }
        }
    }
}

jar {
    enabled = false
}

build.dependsOn shadowJar
